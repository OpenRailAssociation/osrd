plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.ksp)
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    // PLEASE ADD AND UPDATE DEPENDENCIES USING libs.versions.toml
    api project(":kt-osrd-utils")
    api project(':kt-osrd-sim-infra')
    api libs.kotlinx.coroutines.core

    implementation libs.kotlin.logging
    implementation libs.kotlin.stdlib
    implementation libs.kotlinx.coroutines.test
    testImplementation libs.kotlin.test

    // the logging API implementation is only needed during testing, as this is a library
    testRuntimeOnly libs.logback.core
    testRuntimeOnly libs.logback.classic
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
                "-opt-in=kotlin.RequiresOptIn",
                "-opt-in=kotlin.ExperimentalUnsignedTypes",
                "-opt-in=kotlin.time.ExperimentalTime",
                "-opt-in=kotlin.contracts.ExperimentalContracts",
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
        ]
    }
}
